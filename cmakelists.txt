cmake_minimum_required(VERSION 3.28)
project(lavender VERSION 0.0.0.1)

include(CMakeRC.cmake)
include(FetchContent)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED TRUE)
set(CMAKE_EXPORT_COMPILE_COMMANDS TRUE)

if (CMAKE_VERSION VERSION_GREATER 3.30)
    cmake_policy(SET CMP0169 OLD)
endif()

FetchContent_Declare(glfw GIT_REPOSITORY https://github.com/glfw/glfw GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_MakeAvailable(glfw)

FetchContent_Declare(concurrentqueue GIT_REPOSITORY https://github.com/cameron314/concurrentqueue GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_MakeAvailable(concurrentqueue)

FetchContent_Declare(vma GIT_REPOSITORY https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_MakeAvailable(vma)

FetchContent_Declare(offsetAllocator GIT_REPOSITORY https://github.com/sebbbi/offsetAllocator/ GIT_TAG main GIT_SHALLOW TRUE SYSTEM)
FetchContent_Populate(offsetAllocator)

FetchContent_Declare(ctti GIT_REPOSITORY https://github.com/Manu343726/ctti.git GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_Populate(ctti)

FetchContent_Declare(FastNoise2 GIT_REPOSITORY https://github.com/Auburn/FastNoise2/ GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_Populate(FastNoise2)
set(FASTNOISE2_NOISETOOL OFF CACHE BOOL "Build Noise Tool" FORCE) #example if don't need the graph tool
add_subdirectory(${fastnoise2_SOURCE_DIR})

FetchContent_Declare(glm GIT_REPOSITORY https://github.com/g-truc/glm GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_MakeAvailable(glm)
target_compile_definitions(glm INTERFACE GLM_FORCE_DEPTH_ZERO_TO_ONE=1)
target_compile_definitions(glm INTERFACE GLM_SWIZZLE=1)
target_compile_definitions(glm INTERFACE GLM_FORCE_RADIANS=1)
target_compile_definitions(glm INTERFACE GLM_FORCE_SIZE_T_LENGTH=1)
target_compile_definitions(glm INTERFACE GLM_ENABLE_EXPERIMENTAL=1)

FetchContent_Declare(imgui GIT_REPOSITORY https://github.com/ocornut/imgui GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_Populate(imgui)

FetchContent_Declare(plutovg GIT_REPOSITORY https://github.com/sammycage/plutovg.git GIT_TAG main GIT_SHALLOW ON FIND_PACKAGE_ARGS 0.0.3)
FetchContent_MakeAvailable(plutovg)

FetchContent_Declare(plutosvg GIT_REPOSITORY https://github.com/sammycage/plutosvg GIT_TAG master GIT_SHALLOW TRUE SYSTEM)
FetchContent_Populate(plutosvg)





set(Boost_USE_STATIC_LIBS ON) 
set(Boost_USE_MULTITHREADED ON)  
set(Boost_USE_STATIC_RUNTIME ON) 
set(BOOST_ENABLE_CMAKE ON)
set(BOOST_INCLUDE_LIBRARIES container unordered dynamic_bitset core type_traits sort)
message("Downloading boost, this will take some time!")
FetchContent_Declare( Boost GIT_REPOSITORY https://github.com/boostorg/boost.git GIT_TAG boost-1.86.0 GIT_SHALLOW TRUE SYSTEM)
FetchContent_MakeAvailable(Boost)

find_package(Vulkan REQUIRED)
find_package(Freetype REQUIRED)

add_executable(lavender
    src/main.cpp

    src/ecs/entity_component_storage.cpp
    src/ecs/raw_entity.cpp
    
    src/game/camera.cpp
    src/game/frame_generator.cpp
    src/game/game.cpp
    src/game/transform.cpp

    src/gfx/vulkan/allocator.cpp
    src/gfx/vulkan/buffer.cpp
    src/gfx/vulkan/device.cpp
    src/gfx/vulkan/frame_manager.cpp
    src/gfx/vulkan/image.cpp
    src/gfx/vulkan/instance.cpp
    src/gfx/vulkan/swapchain.cpp

    src/gfx/renderer.cpp
    src/gfx/window.cpp


    src/util/index_allocator.cpp
    src/util/log.cpp
    src/util/misc.cpp
    src/util/range_allocator.cpp
    src/util/ranges.cpp
    src/util/static_filesystem.cpp
    src/util/timer.cpp

    src/verdigris/verdigris.cpp
    
)
target_include_directories(lavender PUBLIC src)
target_include_directories(lavender SYSTEM PUBLIC ${vma_SOURCE_DIR}/include)
target_include_directories(lavender SYSTEM PUBLIC ${offsetAllocator_SOURCE_DIR})
target_include_directories(lavender SYSTEM PUBLIC ${ctti_SOURCE_DIR}/include)
target_compile_definitions(lavender PUBLIC LAVENDER_VERSION_MAJOR=${PROJECT_VERSION_MAJOR})
target_compile_definitions(lavender PUBLIC LAVENDER_VERSION_MINOR=${PROJECT_VERSION_MINOR})
target_compile_definitions(lavender PUBLIC LAVENDER_VERSION_PATCH=${PROJECT_VERSION_PATCH})
target_compile_definitions(lavender PUBLIC LAVENDER_VERSION_TWEAK=${PROJECT_VERSION_TWEAK})
if(CMAKE_BUILD_TYPE STREQUAL "Debug" OR CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
    target_compile_definitions(lavender PUBLIC LAVENDER_DEBUG_BUILD=1)
else()
    target_compile_definitions(lavender PUBLIC LAVENDER_DEBUG_BUILD=0)
endif()
target_link_libraries(
    lavender
    PRIVATE
    concurrentqueue
    glm
    Boost::container
    Boost::unordered
    Boost::dynamic_bitset
    Boost::core
    Boost::type_traits
    Boost::sort
    ${FREETYPE_LIBRARIES}
    library_target
)


add_library(library_target STATIC
    ${imgui_SOURCE_DIR}/imgui_demo.cpp
    ${imgui_SOURCE_DIR}/imgui_draw.cpp
    ${imgui_SOURCE_DIR}/imgui_tables.cpp
    ${imgui_SOURCE_DIR}/imgui_widgets.cpp
    ${imgui_SOURCE_DIR}/imgui.cpp

    ${imgui_SOURCE_DIR}/backends/imgui_impl_vulkan.cpp
    ${imgui_SOURCE_DIR}/backends/imgui_impl_glfw.cpp

    ${imgui_SOURCE_DIR}/misc/freetype/imgui_freetype.cpp
    
    ${fastnoise2_SOURCE_DIR}/src/FastNoise/FastNoise_C.cpp
    ${fastnoise2_SOURCE_DIR}/src/FastNoise/Metadata.cpp
    ${fastnoise2_SOURCE_DIR}/src/FastNoise/SmartNode.cpp

    ${plutosvg_SOURCE_DIR}/source/plutosvg.c
)
target_include_directories(library_target SYSTEM PUBLIC ${Vulkan_INCLUDE_DIRS})
target_include_directories(library_target SYSTEM PUBLIC ${imgui_SOURCE_DIR})
target_include_directories(library_target SYSTEM PUBLIC ${FREETYPE_INCLUDE_DIRS})
target_include_directories(library_target SYSTEM PUBLIC ${plutosvg_SOURCE_DIR}/source)
target_include_directories(library_target SYSTEM PUBLIC ${fastnoise2_SOURCE_DIR}/include)
target_compile_definitions(library_target PUBLIC VULKAN_HPP_DISPATCH_LOADER_DYNAMIC=1)
target_compile_definitions(library_target PUBLIC VULKAN_HPP_NO_CONSTRUCTORS=1)
target_compile_definitions(library_target PUBLIC IMGUI_IMPL_VULKAN_NO_PROTOTYPES=1)
target_compile_definitions(library_target PUBLIC IMGUI_USE_WCHAR32=1)
target_compile_definitions(library_target PUBLIC IMGUI_ENABLE_FREETYPE=1)
target_compile_definitions(library_target PUBLIC IMGUI_ENABLE_FREETYPE_PLUTOSVG=1)
target_compile_definitions(library_target PUBLIC PLUTOSVG_HAS_FREETYPE=1)
target_compile_definitions(library_target PUBLIC PLUTOSVG_BUILD_STATIC=1)
target_compile_definitions(library_target PUBLIC VK_NO_PROTOTYPES=1)
target_link_libraries(
    library_target
    PUBLIC 
    glfw
    FastNoise
    plutovg::plutovg
    ${FREETYPE_LIBRARIES}
)


if (CMAKE_CXX_COMPILER_ID STREQUAL "Clang" OR CMAKE_CXX_COMPILER_ID STREQUAL "AppleClang")
    target_compile_options(lavender PUBLIC
        -Weverything
        -Wno-c++98-compat
        -Wno-c++98-compat-pedantic
        -Wno-c++20-compat
        -Wno-pre-c++20-compat-pedantic
        -Wno-covered-switch-default
        -Wno-ctad-maybe-unsupported
        -Wno-pre-c++17-compat
        -Wno-braced-scalar-init
        -Wno-unsafe-buffer-usage
        -Wno-gnu-zero-line-directive
        -Wno-missing-prototypes
        -Wno-switch-enum
        -Wno-undefined-func-template
        -Wno-newline-eof
        -Wno-padded
        -Wno-poison-system-directories
        -Wno-switch-default
        -Wno-weak-vtables
        -Wno-incompatible-pointer-types
        -Wno-nan-infinity-disabled
        -Wno-documentation-unknown-command
    )

    target_compile_options(library_target PUBLIC -Wno-nan-infinity-disabled)
    

    if (CMAKE_BUILD_TYPE STREQUAL "Debug" OR CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
        # Add debug symbols that lldb can actually read
        target_compile_options(lavender PUBLIC -glldb)
        target_compile_options(lavender PUBLIC -gdwarf-4)
        target_compile_options(lavender PUBLIC -g3)

        # target_compile_options(lavender PUBLIC -fsanitize=thread)
        # target_link_options(lavender PUBLIC -fsanitize=thread)

        # target_compile_options(lavender PUBLIC -fsanitize=address)
        # target_link_options(lavender PUBLIC -fsanitize=address)
    endif ()

    target_compile_options(library_target PRIVATE -w) # Disable all warnings

elseif (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
elseif (CMAKE_CXX_COMPILER_ID STREQUAL "Intel")
elseif (CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
    target_compile_options(library_target PRIVATE /W0) # Set warning level to 0
endif()


find_package(Vulkan COMPONENTS glslc)
find_program(glslc_executable NAMES glslc HINTS Vulkan::glslc)
if (APPLE)
    target_link_libraries(lavender PRIVATE "-framework Cocoa -framework IOKit")
    set(CMAKE_CXX_FLAGS "--sysroot ${CMAKE_OSX_SYSROOT} ${CMAKE_CXX_FLAGS}")
endif ()

function(compile_shader target)
    cmake_parse_arguments(PARSE_ARGV 1 arg "" "ENV;FORMAT;INCLUDE_DIRS" "SOURCES")
    foreach (source ${arg_SOURCES})
        add_custom_command(
                OUTPUT ${source}.${arg_FORMAT}
                DEPENDS ${source}
                DEPFILE ${source}.d
                COMMAND
                ${glslc_executable}
                $<$<BOOL:${arg_ENV}>:--target-env=${arg_ENV}>
                $<$<BOOL:${arg_SPV}>:--target-spv=${arg_SPV}>
                $<$<BOOL:${arg_FORMAT}>:-mfmt=${arg_FORMAT}>
                $<$<BOOL:${arg_INCLUDE_DIRS}>:-I${CMAKE_SOURCE_DIR}/src/shaders/include>
                -O
                -g # enable opts & debug symbols
                -MD -MF ${source}.d
                -o ${source}.${arg_FORMAT}
                ${CMAKE_CURRENT_SOURCE_DIR}/${source}
        )
        target_sources(${target} PRIVATE ${source}.${arg_FORMAT})
    endforeach ()
endfunction()

compile_shader(lavender
    ENV vulkan1.2
    SPV spv1.6
    FORMAT bin
    INCLUDE_DIRS "${CMAKE_SOURCE_DIR}/src/shaders/include"
    SOURCES
    src/shaders/triangle.vert
    src/shaders/triangle.frag
    src/shaders/voxel_color_calculation.comp
    src/shaders/voxel_color_transfer.vert
    src/shaders/voxel_color_transfer.frag
    src/shaders/menu_color_transfer.vert
    src/shaders/menu_color_transfer.frag
    src/shaders/skybox.vert
    src/shaders/skybox.frag
    src/shaders/voxel_render.vert
    src/shaders/voxel_render.frag
    src/shaders/voxel_visibility_detection.comp
)

cmrc_add_resource_library(
    lavender_resources
    ALIAS lav2
    NAMESPACE lav
    build/src/shaders/triangle.frag.bin
    build/src/shaders/triangle.vert.bin
    build/src/shaders/voxel_color_calculation.comp.bin
    build/src/shaders/voxel_color_transfer.vert.bin
    build/src/shaders/voxel_color_transfer.frag.bin
    build/src/shaders/menu_color_transfer.vert.bin
    build/src/shaders/menu_color_transfer.frag.bin
    build/src/shaders/skybox.vert.bin
    build/src/shaders/skybox.frag.bin
    build/src/shaders/voxel_render.vert.bin
    build/src/shaders/voxel_render.frag.bin
    build/src/shaders/voxel_visibility_detection.comp.bin
    res/OpenMoji-color-colr1_svg.ttf
    res/unifont-16.0.01.otf
)

target_link_libraries(lavender PRIVATE lav2)